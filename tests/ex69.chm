gc: on
# Garbage Collection

@var_ref_map:BTreeMap = null
@assoc_map:BTreeMap = null
@alloc_map:BTreeMap = null

struct String {
    char: int
    next: String
}

struct Product {
    name: String
    description: String
    owner: String
    price: double
}

struct Student {
    first_initial: int
    last_initial: int
    average: double
    friend: Student
    friend2: Student
}

struct BTreeMap {
    key: long
    value: long
    size: int
    left: BTreeMap
    right: BTreeMap
}

struct StringList {
    string: String
    next: StringList
}

struct AppendUntilResult {
    part: String
    rest: String
}

fun main(): int [
    test_suite()
    return 0
]

fun test_suite() [
    scenario_0()
    gc_mark_and_sweep()
    scenario_1()
    gc_mark_and_sweep()
    print_info()
    scenario_2()
    gc_mark_and_sweep()
    scenario_2B()
    gc_mark_and_sweep()
    scenario_3()
    gc_mark_and_sweep()
    scenario_4()
    gc_mark_and_sweep()
    scenario_5()
    gc_mark_and_sweep()
    scenario_6()
    gc_mark_and_sweep()
    scenario_7()
    gc_mark_and_sweep()
    scenario_8()
    gc_mark_and_sweep()
    scenario_9()
    gc_mark_and_sweep()
    scenario_10()
    gc_mark_and_sweep()
    scenario_11()
    gc_mark_and_sweep()
    scenario_12()
    gc_mark_and_sweep()
    scenario_13()
    gc_mark_and_sweep()
]

fun scenario_0() [
    putchar('0')
    putchar('\n')
    print_info()
]

fun scenario_1() [
    putchar('1')
    putchar('\n')

    bob = alloc Student {
        first_initial = 'M'
        last_initial = 'H'
        average = 100.0
    }
    
    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_2() [
    putchar('2')
    putchar('\n')

    bob = alloc Student {
        first_initial = 'M'
        last_initial = 'H'
        average = 100.0
    }
    
    bob = null

    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_2B() [
    putchar('2')
    putchar('B')
    putchar('\n')

    bob = alloc Student {
        first_initial = 'M'
        last_initial = 'H'
        average = 100.0
    }
    
    robert = bob
    
    bob = null

    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_3() [
    putchar('3')
    putchar('\n')

    bob = alloc Student {
        first_initial = 'M'
        last_initial = 'H'
        average = 100.0
        friend = alloc Student {
            first_initial = 'L'
            last_initial = 'L'
            average = 97.0
        }
    }

    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_4() [
    putchar('4')
    putchar('\n')

    bob = alloc Student {
        first_initial = 'M'
        last_initial = 'H'
        average = 100.0
        friend = alloc Student {
            first_initial = 'L'
            last_initial = 'L'
            average = 97.0
        }
    }
    
    bob.friend = null

    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_5() [
    putchar('5')
    putchar('\n')

    bob = alloc Student {
        first_initial = 'M'
        last_initial = 'H'
        average = 100.0
        friend = alloc Student {
            first_initial = 'L'
            last_initial = 'L'
            average = 97.0
        }
    }
    
    bob = null
    
    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_6() [
    putchar('6')
    putchar('\n')

    bob = alloc Student {
        first_initial = 'M'
        last_initial = 'H'
        average = 100.0
        friend = alloc Student {
            first_initial = 'L'
            last_initial = 'H'
            average = 97.0
        }
        friend2 = alloc Student {
            first_initial = 'E'
            last_initial = 'H'
            average = 80.0
        }
    }
    print_info()
    
    bob = null
    
    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_7() [
    putchar('7')
    putchar('\n')

    message = "Hello"
    
    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_8() [
    putchar('8')
    putchar('\n')

    message = "Hello"
    message = null
    
    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_9() [
    putchar('9')
    putchar('\n')

    print("Hello")
    
    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_10() [
    putchar('1')
    putchar('0')
    putchar('\n')

    print(concat_string("Hello" "World"))
    
    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_11() [
    putchar('1')
    putchar('1')
    putchar('\n')

    print(hello("George"))
    
    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_12() [
    putchar('1')
    putchar('2')
    putchar('\n')

    message = hello("Jessica")
    print(message)
    
    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun scenario_13() [
    putchar('1')
    putchar('3')
    putchar('\n')

    basketball = alloc Product {
        name = "Basketball"
        description = "You can bounce it, you can shoot it!"
        price = 12.50
    }
    basketball.description = "Play it!"
    
    print_info()
    gc_mark_and_sweep()
    print_info()
]

fun print_detailed_info() [
    print_var_ref_map(0)
    print_assoc_map(0)
    print_alloc_map(0)
]

fun print_info() [
    print_var_ref_map_size()
    print_assoc_map_size()
    print_max_assoc_count()
    print_alloc_map_size()
]

fun print_assoc_map_size() [
    putchar('<')
    putchar('A')
    putchar('S')
    putchar('S')
    putchar('O')
    putchar('C')
    putchar('>')
    print_long(btmap_size(@assoc_map))
    putchar('<')
    putchar('/')
    putchar('A')
    putchar('S')
    putchar('S')
    putchar('O')
    putchar('C')
    putchar('>')
    putchar('\n')
]

fun print_var_ref_map_size() [
    putchar('<')
    putchar('V')
    putchar('A')
    putchar('R')
    putchar('R')
    putchar('E')
    putchar('F')
    putchar('S')
    putchar('>')
    print_long(btmap_size(@var_ref_map))
    putchar('<')
    putchar('/')
    putchar('V')
    putchar('A')
    putchar('R')
    putchar('R')
    putchar('E')
    putchar('F')
    putchar('S')
    putchar('>')
    putchar('\n')
]

fun print_alloc_map_size() [
    putchar('<')
    putchar('A')
    putchar('L')
    putchar('L')
    putchar('O')
    putchar('C')
    putchar('>')
    print_long(btmap_size(@alloc_map))
    putchar('<')
    putchar('/')
    putchar('A')
    putchar('L')
    putchar('L')
    putchar('O')
    putchar('C')
    putchar('>')
    putchar('\n')
]

fun print_max_assoc_count() [
    count = max_assoc_count(@assoc_map)
    putchar('<')
    putchar('M')
    putchar('A')
    putchar('X')
    putchar('A')
    putchar('S')
    putchar('S')
    putchar('O')
    putchar('C')
    putchar('>')
    print_long(count)
    putchar('<')
    putchar('/')
    putchar('M')
    putchar('A')
    putchar('X')
    putchar('A')
    putchar('S')
    putchar('S')
    putchar('O')
    putchar('C')
    putchar('>')
    putchar('\n')
]

fun max_assoc_count(map:BTreeMap): int [
    if map == null [
        return 0
    ]
    refs = BTreeMap(map.value)
    my_count = btmap_size(refs)
    left_count = max_assoc_count(map.left)
    right_count = max_assoc_count(map.right)
    if my_count > left_count [
        if my_count > right_count [
            return my_count
        ] else [
            return right_count
        ]
    ] else [
        if right_count > left_count [
            return right_count
        ] else [
            return left_count
        ]
    ]
]

fun print_assoc_map(indent:int) [
    print_indent(indent)
    putchar('<')
    putchar('A')
    putchar('S')
    putchar('S')
    putchar('O')
    putchar('C')
    putchar('>')
    putchar('\n')
    print_assoc_map_helper(@assoc_map indent)
    print_indent(indent)
    putchar('<')
    putchar('/')
    putchar('A')
    putchar('S')
    putchar('S')
    putchar('O')
    putchar('C')
    putchar('>')
    putchar('\n')
]

fun print_var_ref_map(indent:int) [
    putchar('<')
    putchar('V')
    putchar('A')
    putchar('R')
    putchar('R')
    putchar('E')
    putchar('F')
    putchar('S')
    putchar(' ')
    print_long(btmap_size(@var_ref_map))
    putchar('>')
    putchar('\n')
    print_map(@var_ref_map indent + 1)
    putchar('<')
    putchar('/')
    putchar('V')
    putchar('A')
    putchar('R')
    putchar('R')
    putchar('E')
    putchar('F')
    putchar('S')
    putchar('>')
    putchar('\n')
]

fun print_map(map:BTreeMap indent:int) [
    if map == null [
        return
    ]
    print_indent(indent)
    print_long(map.key)
    putchar(' ')
    putchar('>')
    putchar(' ')
    print_long(map.value)
    putchar('\n')
    print_map(map.left indent)
    print_map(map.right indent)
]

fun print_alloc_map(indent:int) [
    print_indent(indent)
    putchar('<')
    putchar('A')
    putchar('L')
    putchar('L')
    putchar('O')
    putchar('C')
    putchar('>')
    putchar('\n')
    print_map(@alloc_map indent + 1)
    print_indent(indent)
    putchar('<')
    putchar('/')
    putchar('A')
    putchar('L')
    putchar('L')
    putchar('O')
    putchar('C')
    putchar('>')
    putchar('\n')
]

fun do_it(thing:String): String [
    message = "H"
    print(message)
    print(thing)
    return message
]

fun hello(subject:String): String [
    return concat_string(concat_string("Hello, " subject) "!")
]


# BTreeMap functions

fun print_indent(amount:int) [
    while amount > 0 [
        putchar(' ')
        putchar(' ')
        putchar(' ')
        putchar(' ')
        amount = amount - 1
    ]
]

fun print_tree(tree:BTreeMap level:int) [
    if tree != null [
        print_indent(level)
        print_long(tree.key)
        putchar(58)
        print_long(tree.value)
        putchar(10)
        print_tree(tree.left level + 1)
        print_tree(tree.right level + 1)
    ]
]

fun print_assoc_map_helper(tree:BTreeMap level:int) [
    sub_map:BTreeMap = null
    if tree != null [
        print_indent(1)
        print_long(tree.key)
        putchar(' ')
        putchar('>')
        putchar(' ')
        sub_map = BTreeMap(tree.value)
        print_map_keys_one_line(sub_map ' ')
        putchar('\n')
        print_assoc_map_helper(tree.left level + 1)
        print_assoc_map_helper(tree.right level + 1)
    ]
]

fun print_map_keys_one_line(map:BTreeMap separator:int) [
    if map == null [
        return
    ]
    print_long(map.key)
    putchar(separator)
    print_map_keys_one_line(map.left separator)
    print_map_keys_one_line(map.right separator)
]

# Garbage collector

fun gc:off gc_add_var_ref(source:long dest:long) [
    if dest == 0 [
        @var_ref_map = btmap_delete(source @var_ref_map)
    ] else [
        @var_ref_map = btmap_set(source dest @var_ref_map)
    ]
]

fun gc:off gc_remove_var_ref(source:long) [
    @var_ref_map = btmap_delete(source @var_ref_map)
]

fun gc:off gc_add_assoc(source:long dest:long) [
    if dest == 0 [
        return
    ]
    result = btmap_get(source @assoc_map)
    refs:BTreeMap = null
    if result != null [
        refs = BTreeMap(result.value)
        new_refs = btmap_set(dest 1 refs)
        @assoc_map = btmap_set(source long(new_refs) @assoc_map)
    ] else [
        refs = btmap(dest 1 null null)
        @assoc_map = btmap_set(source long(refs) @assoc_map)
    ]
]

fun gc:off gc_remove_assoc(source:long dest:long) [
    if dest == 0 [
        return
    ]
    result = btmap_get(source @assoc_map)
    refs:BTreeMap = null
    if result != null [
        refs = BTreeMap(result.value)
        new_refs = btmap_delete(dest refs)
        if new_refs == null [
            @assoc_map = btmap_delete(source @assoc_map)
        ] else [
            @assoc_map = btmap_set(source long(new_refs) @assoc_map)
        ]
    ]
]

fun gc:off gc_mark_and_sweep() [
    putchar('<')
    putchar('G')
    putchar('C')
    putchar('>')
    size_before = btmap_size(@alloc_map)
    # for each entry @alloc_map, set value to false
    @alloc_map = gc_set_all_unmarked(@alloc_map)
    # Mark:
    # for each key/value pair in @var_ref_map walk the references
    # and mark each allocation that was reached
    gc_walk()
    # Sweep: 
    # for each allocation that was not marked, free it
    @alloc_map = gc_free_all_unmarked(@alloc_map)
    size_after = btmap_size(@alloc_map)
    number_freed = size_before - size_after
    putchar('f')
    putchar('r')
    putchar('e')
    putchar('e')
    putchar('d')
    putchar(' ')
    print_long(number_freed)
    putchar('/')
    print_long(size_before)
    putchar('<')
    putchar('/')
    putchar('G')
    putchar('C')
    putchar('>')
    putchar('\n')
]

fun gc:off gc_set_all_unmarked(map:BTreeMap): BTreeMap [
    if map == null [
        return null
    ]
    return btmap(map.key 0 gc_set_all_unmarked(map.left) gc_set_all_unmarked(map.right))
]

fun gc:off gc_walk() [
    gc_walk_var_ref_map(@var_ref_map)
]

fun gc:off gc_walk_var_ref_map(map:BTreeMap) [
    if map == null [
        return
    ]
    gc_visit_alloc(map.value)
    gc_walk_var_ref_map(map.left)
    gc_walk_var_ref_map(map.right)
]

fun gc:off gc_visit_alloc(address:long) [
    alloc_entry = btmap_get(address @alloc_map)
    if alloc_entry != null [
        if alloc_entry.value == 1 [
            # it was already marked, do not re-process
            # or there would be an infinite loop
            return
        ]
        # You've been marked
        alloc_entry.value = 1
        assocs_entry = btmap_get(address @assoc_map)
        if assocs_entry != null [
            assocs = BTreeMap(assocs_entry.value)
            gc_walk_assocs(assocs)
        ]
    ]
]

fun gc:off gc_walk_assocs(assocs:BTreeMap) [
    if assocs == null [
        return
    ]
    
    gc_visit_alloc(assocs.key)
    gc_walk_assocs(assocs.left)
    gc_walk_assocs(assocs.right)
]

fun gc:off gc_free_all_unmarked(map:BTreeMap): BTreeMap [
    if map == null [
        return null
    ]
    left = gc_free_all_unmarked(map.left)
    right = gc_free_all_unmarked(map.right)
    new_map:BTreeMap = null
    if map.value == 0 [
        # Walk did not reach this allocation, free it
        free BTreeMap(map.key)
        # also remove it from @assoc_map
        @assoc_map = btmap_delete(map.key @assoc_map)
        # delete this node
        right_min = btmap_min(right)
        new_right = btmap_delete_min(right)
        if right_min != null [
            new_map = btmap_create_balanced(
                right_min.key
                right_min.value
                left
                new_right
            )
            free right_min
        ] else [
            new_map = left
        ]
    ] else [
        new_map = btmap_create_balanced(map.key map.value left right)
    ]
    free map
    return new_map
]

# Core BTreeMap Operations

fun gc:off btmap_set(key:long value:long btree:BTreeMap): BTreeMap [
    if btree == null [
        return btmap(key value null null)
    ]
    new_tree:BTreeMap = null
    if key == btree.key [
        new_tree = btmap_create_balanced(
            key
            value
            btree.left
            btree.right
        )
    ] else if key > btree.key [
        new_right = btmap_set(key value btree.right)
        new_tree = btmap_create_balanced(
            btree.key 
            btree.value 
            btree.left 
            new_right
        )
    ] else [
        new_left = btmap_set(key value btree.left)
        new_tree = btmap_create_balanced(
            btree.key
            btree.value
            new_left
            btree.right
        )
    ]
    free btree
    return new_tree
]

fun gc:off btmap_get(key:long tree:BTreeMap): BTreeMap [
    if tree == null [
        return null
    ]
    if key == tree.key [
        return tree
    ] else if key > tree.key [
        return btmap_get(key tree.right)
    ]
    return btmap_get(key tree.left)
]

fun gc:off btmap_delete(key:long tree:BTreeMap): BTreeMap [
    if tree == null [
        return null
    ]
    new_tree:BTreeMap = null
    if key == tree.key [
        right_min = btmap_min(tree.right)
        right = btmap_delete_min(tree.right)
        if right_min != null [
            new_tree = btmap_create_balanced(
                 right_min.key
                 right_min.value
                 tree.left
                 right
            )
            free right_min
        ] else [
            new_tree = tree.left
        ]
    ] else if key > tree.key [
        new_tree = btmap_create_balanced(
            tree.key
            tree.value
            tree.left
            btmap_delete(key tree.right)
        )
    ] else [
        new_tree = btmap_create_balanced(
            tree.key
            tree.value
            btmap_delete(key tree.left)
            tree.right
        )
    ]
    free tree
    return new_tree
]

fun gc:off btmap_min(tree:BTreeMap): BTreeMap [
    if tree == null [
        return null
    ] else [
        if tree.left != null [
            return btmap_min(tree.left)
        ]
        return tree
    ]
]

fun gc:off btmap_delete_min(tree:BTreeMap): BTreeMap [
    if tree == null [
        return null
    ]
    new_tree:BTreeMap = null
    if tree.left != null [
        new_tree = btmap(
            tree.key
            tree.value
            btmap_delete_min(tree.left)
            tree.right
        )
    ] else [
        new_tree = tree.right
    ]
    return new_tree
]

fun gc:off btmap(
    key:long
    value:long
    left:BTreeMap
    right:BTreeMap
): BTreeMap [
    size = 1
    if left != null [
        size = size + left.size
    ]
    if right != null [
        size = size + right.size
    ]
    return alloc BTreeMap {
        key = key
        value = value
        size = size
        left = left
        right = right
    }
]

# See http://groups.csail.mit.edu/mac/users/adams/BB/
# section 6: Balance maintenance algorithms
fun gc:off btmap_single_left_rotation(a:BTreeMap): BTreeMap [
    x = a.left
    b = a.right
    y = a.right.left
    z = a.right.right
    a1 = btmap(a.key a.value x y)
    b1 = btmap(b.key b.value a1 z)
    free a
    free b
    return b1
]

fun gc:off btmap_single_right_rotation(b:BTreeMap): BTreeMap [
    a = b.left
    x = a.left
    y = a.right
    z = b.right
    b1 = btmap(b.key b.value y z)
    a1 = btmap(a.key a.value x b1)
    free a
    free b
    return a1
]

fun gc:off btmap_double_left_rotation(a:BTreeMap): BTreeMap [
    x = a.left
    c = a.right
    b = c.left
    z = c.right
    y1 = b.left
    y2 = b.right
    a1 = btmap(a.key a.value x y1)
    c1 = btmap(c.key c.value y2 z)
    b1 = btmap(b.key b.value a1 c1)
    free a
    free b
    free c
    return b1
]

fun gc:off btmap_double_right_rotation(c:BTreeMap): BTreeMap [
    z = c.right
    a = c.left
    x = a.left
    b = a.right
    y1 = b.left
    y2 = b.right
    a1 = btmap(a.key a.value x y1)
    c1 = btmap(c.key c.value y2 z)
    b1 = btmap(b.key b.value a1 c1)
    free a
    free b
    free c
    return b1
]

fun gc:off btmap_create_balanced(
    key:long
    value:long
    left:BTreeMap
    right:BTreeMap
): BTreeMap [
    weight = 3.745
    p = btmap(key value left right)
    ln = btmap_size(left)
    rn = btmap_size(right)
    if ln + rn < 2 [
        return p
    ] else if rn > int(weight * double(ln)) [
        # right is too big
        rl = right.left
        rr = right.right
        rln = btmap_size(rl)
        rrn = btmap_size(rr)
        if rln < rrn [
            return btmap_single_left_rotation(p)
        ] else [
            return btmap_double_left_rotation(p)
        ]
    ] else if ln > int(weight * double(rn)) [
        # left is too big
        ll = left.left
        lr = left.right
        lln = btmap_size(ll)
        lrn = btmap_size(lr)
        if lrn < lln [
            return btmap_single_right_rotation(p)
        ] else [
            return btmap_double_right_rotation(p)
        ]
    ] else [
        return p
    ]
]

fun gc:off btmap_size(tree:BTreeMap): int [
    if tree == null [
        return 0
    ] else [
        return tree.size
    ]
]

# String functions ==========================

fun string(char:int next:String): String [
    return alloc String {
        char = char
        next = next
    }
]

fun string_list(string:String next:StringList): StringList [
    return alloc StringList {
        string = string
        next = next
    }
]

fun put_string(string:String) [
    if string != null [
        putchar(string.char)
        put_string(string.next)
    ]
]

fun split_string(source:String separator:int): StringList [
    return split_string_helper(source separator null)
]

fun split_string_helper(source:String separator:int dest:StringList): StringList [
    if source == null [
        return dest
    ]
    append_result = append_until(source separator null)
    rest_list = split_string_helper(append_result.rest separator dest)
    return string_list(append_result.part rest_list)
]

fun append_until(source:String stop_char:int dest:String): AppendUntilResult [
    if source == null [
        return alloc AppendUntilResult {
            part = dest
            rest = null
        }
    ]
    char = source.char
    if char == stop_char [
        return alloc AppendUntilResult {
            part = dest
            rest = source.next
        }
    ] else [
        append_result = append_until(source.next stop_char dest)
        return alloc AppendUntilResult {
            part = string(char append_result.part)
            rest = append_result.rest
        }
    ]
]

fun concat_string(string1:String string2:String): String [
    combined:String = null
    combined = concat_string_helper(string2 combined)
    combined = concat_string_helper(string1 combined)
    return combined
]

fun concat_string_helper(source:String dest:String): String [
    new_dest:String = null
    if source != null [
        char = source.char
        new_dest = concat_string_helper(source.next dest)
        return string(char new_dest)
    ] else [
        return dest
    ]
]

fun string_length(string:String): int [
    length:int = 0
    curr = string
    while curr != null [
        curr = curr.next
        length = length + 1
    ]
    return length
]

fun find_string(string:String target:String): String [
    if starts_with_string(string target) [
        return string
    ] else [
        return find_string(string.next target)
    ]
]

fun starts_with_string(string:String target:String): bool [
    if target == null [
        return true
    ]
    if string == null [
        return false
    ]
    if string.char == target.char [
        return starts_with_string(string.next target.next)
    ] else [
        return false
    ]
]

fun print(string:String) [
    put_string(string)
    putchar('\n')
]

# Number functions

fun print_long(num:long) [
    print_long_helper(num true)
]

fun print_long_helper(num:long first:bool) [
    if num > 0 [
        digit = num % 10
        print_long_helper(num / 10 false)
        putchar('0' + digit)
    ] else [
        if first [
            putchar('0')
        ]
    ]
]

fun read_long(): long [
    num = 0
    while true [
        digit = getchar()
        if digit == 10 [
            break
        ]
        num = num * 10
        num = num + (digit - 48)
    ]
    return num
]
